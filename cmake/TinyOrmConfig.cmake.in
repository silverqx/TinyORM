# Config file for the @PROJECT_NAME@ package
# It defines the @TinyOrm_ns@::@TinyOrm_target@ target to link against

@PACKAGE_INIT@

list(APPEND CMAKE_MESSAGE_CONTEXT @TinyOrm_target@ CF)

# Information about build type/s, used in the info message
include("${CMAKE_CURRENT_LIST_DIR}/Modules/TinyPackageConfigHelpers.cmake")
tiny_get_build_types(tiny_build_type @cvf_is_multi_config@ @cvf_is_vcpkg@)

# Info message about found package
message(STATUS "Found package ${CMAKE_FIND_PACKAGE_NAME} \
${${CMAKE_FIND_PACKAGE_NAME}_VERSION}${tiny_build_type} (requested \
${${CMAKE_FIND_PACKAGE_NAME}_FIND_VERSION_COMPLETE}) at ${CMAKE_CURRENT_LIST_FILE}")
unset(tiny_build_type)

message(CHECK_START "Configuring @PROJECT_NAME@ package")
list(APPEND CMAKE_MESSAGE_INDENT "  ")

set(@TinyOrm_ns@_BIN_DIR "@PACKAGE_BIN_INSTALL_DIR@")
set_and_check(@TinyOrm_ns@_CONFIG_DIR "@PACKAGE_CONFIG_INSTALL_DIR@")
set_and_check(@TinyOrm_ns@_DOC_DIR "@PACKAGE_DOC_INSTALL_DIR@")
set_and_check(@TinyOrm_ns@_INCLUDE_DIR "@PACKAGE_INCLUDE_INSTALL_DIR@")
set_and_check(@TinyOrm_ns@_LIB_DIR "@PACKAGE_LIB_INSTALL_DIR@")

@tiny_cmake_module_path@

include(CMakeFindDependencyMacro)
@tiny_find_dependency_calls@

include("${CMAKE_CURRENT_LIST_DIR}/TinyOrmTargets.cmake")

# Set up meaningful values for MAP_IMPORTED_CONFIG_<CONFIG>, these are good defaults and
# it helps to avoid link a release type builds against a debug build
set(tiny_map_release Release RelWithDebInfo MinSizeRel)
set(tiny_map_relwithdebinfo RelWithDebInfo Release MinSizeRel)
set(tiny_map_minsizerel MinSizeRel RelWithDebInfo Release)
# MSVC runtime library crashes if you do not link a debug build against a debug build
if(MSVC)
    set(tiny_map_debug Debug)
else()
    set(tiny_map_debug Debug Debug RelWithDebInfo Release MinSizeRel)
endif()

set_target_properties(@TinyOrm_ns@::@TinyOrm_target@ PROPERTIES
    MAP_IMPORTED_CONFIG_RELEASE "${tiny_map_release}"
    MAP_IMPORTED_CONFIG_RELWITHDEBINFO "${tiny_map_relwithdebinfo}"
    MAP_IMPORTED_CONFIG_MINSIZEREL "${tiny_map_minsizerel}"
    MAP_IMPORTED_CONFIG_DEBUG "${tiny_map_debug}"
)

message(DEBUG "Setting CMAKE_MAP_IMPORTED_CONFIG_<CONFIG> for the \
@TinyOrm_ns@::@TinyOrm_target@ target to avoid link a release type builds against \
a debug build

 * CMAKE_MAP_IMPORTED_CONFIG_RELEASE        = ${tiny_map_release}
 * CMAKE_MAP_IMPORTED_CONFIG_RELWITHDEBINFO = ${tiny_map_relwithdebinfo}
 * CMAKE_MAP_IMPORTED_CONFIG_MINSIZEREL     = ${tiny_map_minsizerel}
 * CMAKE_MAP_IMPORTED_CONFIG_DEBUG          = ${tiny_map_debug}
")

unset(tiny_map_release)
unset(tiny_map_relwithdebinfo)
unset(tiny_map_minsizerel)
unset(tiny_map_debug)

check_required_components(@TinyOrm_ns@)

list(POP_BACK CMAKE_MESSAGE_INDENT)
message(CHECK_PASS "done")

list(POP_BACK CMAKE_MESSAGE_CONTEXT)
list(POP_BACK CMAKE_MESSAGE_CONTEXT)
